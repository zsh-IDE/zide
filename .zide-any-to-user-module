# -*- mode: sh; sh-indentation: 4; indent-tabs-mode: nil; sh-basic-offset: 4; -*-

# Copyright 2019 Sebastian Gniazdowski
# License: MIT

# Allows elastic module-spec across the code.
#
# $1 - module spec (4 formats: user---module, user/module, user, module)
# $2 - module (only when $1 - i.e. user - given)
#
# Returns user and module in $reply

# Two components given?
# That's a pretty fast track to call this function this way
if [[ -n "$2" ]];then
    2=${~2}
    reply=( "${1:-${${(M)2#/}:+%}}" "${${${(M)1#%}:+$2}:-${2//---//}}" )
    return 0
fi

# Is it absolute path?
if [[ "$1" = "/"* ]]; then
    reply=( "%" "$1" )
    return 0
fi

# Is it absolute path in %the-path format?
if [[ "$1" = "%"* ]]; then
    reply=( "%" "${${1/\%HOME/$HOME}#%}" )
    reply[2]=${~reply[2]}
    return 0
fi

# Rest is for single component given
# It doesn't touch $2

if [[ "$1" = */* ]]; then
    reply=( "${1%%/*}" "${1#*/}" )
    return 0
fi

reply=( "${${(M)1#*---}%---}" "${${${1#*---}//---//}:-_unknown}" )

return 0

# vim:ft=zsh:sw=4:sts=4:et
